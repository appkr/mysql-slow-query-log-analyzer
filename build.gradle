plugins {
    id 'java-library'
    id 'idea'
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}" apply false
    id 'com.avast.gradle.docker-compose' version "${dockerComposeVersion}"
    id 'io.freefair.lombok' version "${lombokVersion}"
    id 'io.freefair.maven-optional' version '6.6.2'
    id 'com.diffplug.spotless' version "${spotlessVersion}"
    id 'com.gorylenko.gradle-git-properties' version "${gitPropertiesVersion}"
}

wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    group = 'dev.appkr'
    version = project.version

    repositories {
        mavenCentral()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {
    test {
        useJUnitPlatform()
    }
}

def dockerComposePath = System.getProperty('os.arch').equals('aarch64')
        ? 'docker/compose-arm.yaml' : 'docker/compose-intel.yaml'
dockerCompose {
    useComposeFiles = [dockerComposePath]
}

spotless {
    encoding 'UTF-8'
    java {
        targetExclude("**/build/**")
        importOrder()
        removeUnusedImports()
        endWithNewline()
    }
}

compileJava.dependsOn('spotlessApply')
